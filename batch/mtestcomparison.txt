displaycode off
displayresult off
integer i j k t datacount fp tp fn tn counts[3,10]
file file1[11] file2[11] file3[11]
real split tpr fpr error
string f1 f2
twotailed
= datacount 100
real positives[%datacount%] negatives[%datacount%]
for k 1 10
  = counts[1,%k%] 0
  = counts[2,%k%] 0
  = counts[3,%k%] 0
endfor k
for k 1 100
  randomize %k%
  for t 0 10
    = file1[%t+1%] multi%t%.fpfn
    openfile file1[%t+1%] w
    = file2[%t+1%] multi%t%.tprfpr
    openfile file2[%t+1%] w
    = file3[%t+1%] multi%t%.error
    openfile file3[%t+1%] w
  endfor t
  for t 1 10
    for i 1 %datacount%
      random 1 6
      = positives[%i%] %out1%
      random 3 8
      = negatives[%i%] %out1%
    endfor i
    for i 0 10
      = fp 0
      = fn 0
      = tp 0
      = tn 0
      = split %4+(0.01*i)%
      for j 1 %datacount%
        if < positives[%j%] %split% 
          ++ tp
        else
          ++ fn
        endif
        if > negatives[%j%] %split% 
          ++ tn
        else
          ++ fp
        endif
      endfor j
      = tpr %(tp+0.0)/(tp+fn)%
      = fpr %(fp+0.0)/(fp+tn)%
      = error %(fp+fn+0.0)/(fp+fn+tp+tn)%
      writefile file1[%i+1%] %fp%_%fn%
      writefile file2[%i+1%] %tpr%_%fpr%
      writefile file3[%i+1%] %error%
    endfor i
  endfor t
  for t 0 10
    closefile file1[%t+1%]
    closefile file2[%t+1%]
    closefile file3[%t+1%]
  endfor t
  for t 1 10
    hotellingt multi0.fpfn multi%t%.fpfn
    if = out1 0
      ++ counts[1,%t%]
    endif
    hotellingt multi0.tprfpr multi%t%.tprfpr
    if = out1 0
      ++ counts[2,%t%]
    endif
    pairedttest multi0.error multi%t%.error
    if = out1 0
      ++ counts[3,%t%]
    endif
  endfor t
  writescreen %k%
endfor k
for k 1 3
  output %k%.txt
  for t 1 10
    write %t%_%counts[k,t]/100.0%
  endfor t
endfor k
output stdout
